// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static int keyboard_pointer;

    /** Initializes the keyboard. */
    function void init() {
        let keyboard_pointer = 24576;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(keyboard_pointer);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char pressed_key, temp;
        
        let pressed_key = Keyboard.keyPressed();
        
        while (true) {
            let pressed_key = Keyboard.keyPressed();

            if (pressed_key > 0) {

                while (true) {
                    let temp = Keyboard.keyPressed();

                    if (temp = 0) {
                        if (~(pressed_key=128) & ~(pressed_key=129)) {
                            do Output.printChar(pressed_key);
                        } else {
                            
                            if (pressed_key=128) {
                                do Output.println();
                            }
                        }
                        return pressed_key;
                    }
                }
            }
        }

        return pressed_key;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var char pressed_key;
        var String line;

        do Output.printString(message);
        let line = String.new(80);

        while (true) {
            let pressed_key = Keyboard.readChar();

            if (pressed_key = 128) {
                return line;
            }
            else {
                if (pressed_key = 129) {
                    do line.eraseLastChar();
                    do Output.backSpace();
                }
                else {
                    let line = line.appendChar(pressed_key);
                }
            }
        }

        return line;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String string;

        let string = Keyboard.readLine(message);

        return string.intValue();
    }
}
